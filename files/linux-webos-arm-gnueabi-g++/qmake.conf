#
# qmake configuration for building with arm-webos-linux-gnueabi-g++
#

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/g++-unix.conf)

PalmPDK = /opt/PalmPDK
QT5_WEBOS_PATH = /home/nizovn/webos/qt5/qt5.9
TOOLCHAIN_PATH = $$QT5_WEBOS_PATH/gcc-linaro-4.8-2015.06-x86_64_arm-linux-gnueabi/bin
SYSROOT = $$QT5_WEBOS_PATH/gcc-linaro-4.8-2015.06-x86_64_arm-linux-gnueabi/arm-linux-gnueabi/lib

QMAKE_INCDIR += $$QT5_WEBOS_PATH/device/include
QMAKE_LIBDIR += $$QT5_WEBOS_PATH/device/lib

QMAKE_INCDIR += $$PalmPDK/include
QMAKE_INCDIR += $$PalmPDK/include/SDL
QMAKE_LIBDIR += $$SYSROOT
QMAKE_LIBDIR += $$PalmPDK/device/lib

QMAKE_LFLAGS += -Wl,--allow-shlib-undefined

QT5_RPATH = /media/cryptofs/apps/usr/palm/applications/com.nizovn.qt5/lib
GLIBC_RPATH = /media/cryptofs/apps/usr/palm/applications/com.nizovn.glibc/lib
DBUS_RPATH = /media/cryptofs/apps/usr/palm/applications/org.webosinternals.dbus/lib
OPENSSL_RPATH = /media/cryptofs/apps/usr/palm/applications/com.nizovn.openssl/lib

QMAKE_LFLAGS += -Wl,--dynamic-linker=$$GLIBC_RPATH/ld.so
QMAKE_RPATHDIR += $$GLIBC_RPATH
QMAKE_RPATHDIR += $$QT5_RPATH
QMAKE_RPATHDIR += $$DBUS_RPATH
QMAKE_RPATHDIR += $$OPENSSL_RPATH

QMAKE_CFLAGS += -O2 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp -ftree-vectorize
QMAKE_CXXFLAGS += -O2 -march=armv7-a -mtune=cortex-a8 -mfpu=neon -mfloat-abi=softfp -ftree-vectorize
DEFINES += Q_OS_WEBOS

# modifications to g++.conf
QMAKE_CC                = $$TOOLCHAIN_PATH/arm-linux-gnueabi-gcc
QMAKE_CXX               = $$TOOLCHAIN_PATH/arm-linux-gnueabi-g++
QMAKE_LINK              = $$TOOLCHAIN_PATH/arm-linux-gnueabi-g++
QMAKE_LINK_SHLIB        = $$TOOLCHAIN_PATH/arm-linux-gnueabi-g++

# modifications to linux.conf
QMAKE_AR                = $$TOOLCHAIN_PATH/arm-linux-gnueabi-ar cqs
QMAKE_OBJCOPY           = $$TOOLCHAIN_PATH/arm-linux-gnueabi-objcopy
QMAKE_NM                = $$TOOLCHAIN_PATH/arm-linux-gnueabi-nm -P
QMAKE_STRIP             = $$TOOLCHAIN_PATH/arm-linux-gnueabi-strip
load(qt_config)
